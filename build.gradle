buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.4.14"
}

wrapper {
    gradleVersion = '6.0.1'
}

group 'antlr'
version pluginVersion

apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'antlr'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

intellij {
    version = ideaVersion

    pluginName 'antlr-intellij-plugin-v4'
    downloadSources true
    updateSinceUntilBuild false

    // TODO we only need this for unit tests which extend `LightCodeInsightFixtureTestCase`.
    //  This class is in the Java plugin in recent versions, and `BasePlatformTestCase` which is
    //  the non-Java equivalent does not exist in versions < 2019.2.
    plugins 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    antlr("org.antlr:antlr4:$antlr4Version") { // use ANTLR version 4
        exclude group:'com.ibm.icu', module:'icu4j'
    }
    compile "org.antlr:antlr4-intellij-adaptor:0.1"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.1.0'

}

generateGrammarSource {
    arguments += ["-package", "org.antlr.intellij.plugin.parser", "-Xexact-output-dir"]
}

tasks.withType(JavaCompile) {
    if (it.name == "compileJava") {
        classpath = classpath.filter {
            it.name != "java-api.jar" && it.name != "java-impl.jar"
        }
    }
}