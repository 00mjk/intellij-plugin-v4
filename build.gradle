buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.3.12"
}

wrapper {
    gradleVersion = '4.10.2'
}

group 'antlr'
version pluginVersion

apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'antlr'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

intellij {
    version = ideaVersion

    pluginName 'antlr-intellij-plugin-v4'
    downloadSources true
    updateSinceUntilBuild false
}

sourceSets {
    main {
        java {
            srcDirs += ['src/java', 'src/adaptor']
        }
        resources {
            srcDirs = ['resources', 'resources/icons', 'resources/templates', 'resources/liveTemplates', 'resources/colorSchemes']
        }
        antlr {
            srcDirs = ['src/grammars']
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    antlr("org.antlr:antlr4:$antlr4Version") { // use ANTLR version 4
        exclude group:'com.ibm.icu', module:'icu4j'
    }
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

generateGrammarSource {
    arguments += ["-package", "org.antlr.intellij.plugin.parser", "-Xexact-output-dir"]
}